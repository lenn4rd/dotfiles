# load zgen
source "${HOME}/.zgen/zgen.zsh"

# check if there's no init script
if ! zgen saved; then
  echo "Creating a zgen save"

  # Use plugins
  zgen load jonmosco/kube-ps1
  zgen load lenn4rd/zsh-iterm-touchbar
  zgen load mafredri/zsh-async
  zgen load mattberther/zsh-pyenv
  zgen load rupa/z

  # Use dotfiles
  export DOTFILES=$HOME/.dotfiles

  # all of our zsh files
  typeset -U config_files
  config_files=($DOTFILES/**/*.zsh)

  # load the path files
  for file in ${(M)config_files:#*/path.zsh}; do
    file=${file#${DOTFILES}/}

    zgen load $HOME/.dotfiles $file
  done

  # load everything but the path and completion files
  for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
    file=${file#${DOTFILES}/}

    zgen load $HOME/.dotfiles $file
  done

  # load every completion after autocomplete loads
  for file in ${(M)config_files:#*/completion.zsh}; do
    file=${file#${DOTFILES}/}

    zgen load $HOME/.dotfiles $file
  done

  # Use completions
  zgen load rhysd/zsh-bundle-exec
  zgen load zsh-users/zsh-completions src
  zgen load zsh-users/zsh-syntax-highlighting

  # Stash your environment variables in ~/.localrc. This means they'll stay out
  # of your main dotfiles repository (which may be public, like this one), but
  # you'll have access to them in your scripts.
  if [[ -a ~/.localrc ]]; then
    zgen load ~/.localrc
  fi

  # save all to init script
  zgen save
fi

# Set theme
zgen load sindresorhus/pure
